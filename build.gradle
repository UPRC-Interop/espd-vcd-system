subprojects { project ->
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'

    version = '1.0.0'
    group = 'eu.esens.espdvcd'

    project.ext.slf4j_version = '1.7.13'
    project.ext.log4j2_version = '2.4.1'
    project.ext.jaxb_impl_version = '2.2.11'
    project.ext.jaxb_api_version = '2.2.12'
    project.ext.hibernate_version = '5.0.3.Final'
    project.ext.postgresql_version = '9.4-1205-jdbc42'
    project.ext.h2_version = '1.4.190'

    project.ext.libraries = [
        /* logging */
        slf4j_api       : "org.slf4j:slf4j-api:${project.slf4j_version}",
        log4j_slf4j_impl: "org.apache.logging.log4j:log4j-slf4j-impl:${project.log4j2_version}",
        log4j_core      : "org.apache.logging.log4j:log4j-core:${project.log4j2_version}",
        log4j_api       : "org.apache.logging.log4j:log4j-api:${project.log4j2_version}"
    ]   

    repositories {
        mavenCentral()
        jcenter()
    }
        
    publishing {
        publications {
            myPublication (MavenPublication) {
                from components.java
            }
        }
        repositories {
            maven { 
                credentials {
                    username "$nexusUsername"
                    password "$nexusPassword"
                }
                url "http://nexus.e-sens.gr/content/repositories/${project.version.endsWith('-SNAPSHOT') ? 'snapshots' : 'releases' }/"
            }

        }
    }  
}
// 
// global definition of compile dependency
// for project logging
// everywhere except in-memory-db and logging projects
configure(subprojects)
   // - project(':logging'))
{
    dependencies {
        compile libraries.slf4j_api
        compile libraries.log4j_slf4j_impl
        compile libraries.log4j_core
        compile libraries.log4j_api       
    }
    dependencies {
        compile 'com.google.guava:guava:14.0'
    }
    
}

// global definition of testCompile dependency
// for external library junit
// everywhere except a few projects
configure(subprojects
  //  - project(':logging')
) {
    dependencies {
        testCompile 'junit:junit:4.12'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.13'
}



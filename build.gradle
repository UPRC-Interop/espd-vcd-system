/**
 * Author: Panagiotis NICOLAOU
 * Email: pnikola@unipi.gr
 */
// build aliases: define a synonym here if you want a shortcut to run multiple targets
def buildAliases = [
        'rebuild': ['clean', 'xjc', 'build', 'ui:jettyStart']
]
def expandedTaskList = []
gradle.startParameter.taskNames.each {
    expandedTaskList << (buildAliases[it] ? buildAliases[it] : it)
}
gradle.startParameter.taskNames = expandedTaskList.flatten()
println "\n${gradle.startParameter.taskNames}\n"

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'

    version = '1.0.0-SNAPSHOT'
    group = 'eu.esens.espdvcd'

    project.ext.slf4j_version = '1.7.13'
    project.ext.log4j2_version = '2.4.1'
    project.ext.jaxb_impl_version = '2.2.7'
    project.ext.jaxb_api_version = '2.2.12'
    project.ext.hibernate_version = '5.0.3.Final'
    project.ext.postgresql_version = '9.4-1205-jdbc42'

    project.ext.libraries = [
            /* logging */
            slf4j_api       : "org.slf4j:slf4j-api:${project.slf4j_version}",
            log4j_slf4j_impl: "org.apache.logging.log4j:log4j-slf4j-impl:${project.log4j2_version}",
            log4j_core      : "org.apache.logging.log4j:log4j-core:${project.log4j2_version}",
            log4j_api       : "org.apache.logging.log4j:log4j-api:${project.log4j2_version}",

            /* hibernate */
            hibernate_core  : "org.hibernate:hibernate-core:${project.hibernate_version}",

            /* postgresql */
            postgresql      : "org.postgresql:postgresql:${project.postgresql_version}",
    ]

    repositories {
//        mavenLocal()
        mavenCentral()
    }

    dependencies {
    }
}

// global definition of compile dependency
// for project logging
// everywhere except in-memory-db and logging projects
configure(subprojects
        - project(':logging')
        - project(':ui')) {
    dependencies {
        compile project(':logging')
    }
}

// global definition of testCompile dependency
// for external library junit
// everywhere except a few projects
configure(subprojects
        - project(':logging')
        - project(':persistence')
        - project(':schema')) {
    dependencies {
        testCompile 'junit:junit:4.12'
    }
}